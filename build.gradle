import com.kneelawk.spirv.SpirvCompilerPlugin
import org.gradle.internal.os.OperatingSystem

plugins {
    id 'java'
    id 'org.javamodularity.moduleplugin' version '1.5.0'
    id 'org.beryx.jlink' version '2.6.6'
    id 'application'
}

apply plugin: SpirvCompilerPlugin

version = '0.0.1'

sourceCompatibility = 11

mainClassName = "$moduleName/com.kneelawk.hellovulkan.HelloVulkan"

project.ext.lwjglVersion = "3.2.1"
project.ext.jomlVersion = "1.9.13"

switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
        project.ext.lwjglNatives = "natives-linux"
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = "natives-macos"
        break
    case OperatingSystem.WINDOWS:
        project.ext.lwjglNatives = "natives-windows"
        break
}

project.ext.nativeModules = 'org.lwjgl.natives,org.lwjgl.glfw.natives'
if (lwjglNatives == 'natives-macos') project.ext.nativeModules += ',org.lwjgl.vulkan.natives'

sourceSets {
    simplefrag
    simplevert
}

repositories {
	jcenter()
}

dependencies {
	implementation "org.joml:joml:${jomlVersion}"
    implementation "com.google.guava:guava:27.0.1-jre"

	implementation "org.lwjgl:lwjgl:${lwjglVersion}"
	implementation "org.lwjgl:lwjgl-assimp:${lwjglVersion}"
	implementation "org.lwjgl:lwjgl-glfw:${lwjglVersion}"
	implementation "org.lwjgl:lwjgl-jemalloc:${lwjglVersion}"
	implementation "org.lwjgl:lwjgl-openal:${lwjglVersion}"
	implementation "org.lwjgl:lwjgl-stb:${lwjglVersion}"
	implementation "org.lwjgl:lwjgl-vulkan:${lwjglVersion}"

    runtime "org.lwjgl:lwjgl:${lwjglVersion}:${lwjglNatives}"
    runtime "org.lwjgl:lwjgl-assimp:${lwjglVersion}:${lwjglNatives}"
    runtime "org.lwjgl:lwjgl-glfw:${lwjglVersion}:${lwjglNatives}"
    runtime "org.lwjgl:lwjgl-jemalloc:${lwjglVersion}:${lwjglNatives}"
    runtime "org.lwjgl:lwjgl-openal:${lwjglVersion}:${lwjglNatives}"
    runtime "org.lwjgl:lwjgl-stb:${lwjglVersion}:${lwjglNatives}"
    if (lwjglNatives == 'natives-macos') runtime "org.lwjgl:lwjgl-vulkan:${lwjglVersion}:${lwjglNatives}"
}

run {
    jvmArgs('--add-modules', nativeModules)
}

jlink {
    options = ['--compress', '2', '--no-header-files', '--no-man-pages', '--add-modules', nativeModules]
    launcher {
        name = 'HelloVulkan'
        jvmArgs = ['--add-modules', nativeModules]
    }
}

processResources {
    dependsOn(compileSimplevertSpirv, compileSimplefragSpirv)
    from(sourceSets.simplevert.spirv.outputDir, sourceSets.simplefrag.spirv.outputDir)
}
